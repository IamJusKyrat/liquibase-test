buildscript {
	ext {
		springBootVersion = '2.2.5.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://repo.maven.apache.org/maven2' }
		maven { url 'https://repo.spring.io/milestone' }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.2.0'
		classpath "org.liquibase:liquibase-gradle-plugin:2.0.2"
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'

group 'com.test.liquibase'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	springCloudVersion = 'Greenwich.SR2'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}


dependencies {
	implementation project(':liquibase-library')
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'javax.servlet:javax.servlet-api:4.0.1'
	implementation 'org.slf4j:slf4j-api:1.7.25'

	runtime("mysql:mysql-connector-java")

	liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
	liquibaseRuntime 'mysql:mysql-connector-java:8.0.19'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
	liquibaseRuntime 'org.slf4j:slf4j-api:1.7.25'
	liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.3'

	liquibaseRuntime sourceSets.main.output

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

project.ext.mainUrl = 'jdbc:mysql://localhost/application'
project.ext.mainUsername = 'root'
project.ext.mainPassword = 'root'
project.ext.changelogFile = 'src/main/resources/db/db.changelog-master.xml'
project.ext.runList = 'main'

tasks.install.dependsOn(bootJar)

liquibase {
	activities {
		main {
			changeLogFile project.ext.changelogFile
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
		}
		diffMain {
			changeLogFile project.ext.changelogFile
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
		}
	}
	runList = project.ext.runList
}
